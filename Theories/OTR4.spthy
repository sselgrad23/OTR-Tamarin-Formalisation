theory OTR4
begin

builtins: 
  symmetric-encryption,
  diffie-hellman, 
  signing,
  hashing

/*** Key Infrastructure ***/

/* Register signing/verification key pair. 
 * ltk is the secret signing key
 * pk(ltk) is the public verification key
 */
rule Register_key_pair:
  [ Fr(~ltk) ]
--[]->
  [ !Ltk($A, ~ltk)
  , !Pk($A, pk(~ltk))
  , Out(pk(~ltk))
  ]

// Agent is compromised and its secret signing key is revealed
rule Reveal_ltk:
  [ !Ltk(A, ltk) ]
--[ Reveal(A) ]->
  [ Out(ltk) ]

/*** Formalise SIGMA ***
 * 1. A -> B : g^x
 * 2. B -> A : g^y
 * 3. A -> B : "A", {g^y, g^x}sk_A, ["0", "A"]K_m, vk_A
 * 4. B -> A : "B", {g^x, g^y}sk_B, ["1", "B"]K_m, vk_B
 */

// Initialise thread for initiator role
rule Init_A:
  [ Fr(~id) 
  , !Ltk(A, ltkA) // A's signing key
  , !Pk(B, pkB)   // B's verification key
  ]
--[ Create_A(A, ~id) ]->
  [ St_A_1(A, ~id, ltkA, pkB, B) ]

// Initialise thread for responder role
rule Init_B:
  [ Fr(~id)
  , !Ltk(B, ltkB) // B's signing key
  , !Pk(A, pkA)   // A's verification key
  ]
--[ Create_B(B, ~id) ]->
  [ St_B_1(B, ~id, ltkB, pkA, A) ]

// Step 1.1: A sends g^x to B
rule A_1_send:
  let m = 'g'^~x
  in
  [ St_A_1(A, id, ltkA, pkB, B) 
  , Fr(~x)
  ]
--[ Send(A, m) ]->
  [ St_A_2(A, id, ltkA, pkB, B, ~x)
  , Out(m)
  ]

// Step 1.2: B receives g^x from A
rule B_1_receive:
  let m = gx
  in
  [ St_B_1(B, id, ltkB, pkA, A)
  , In(m)
  ]
--[ Recv(B, m) 
  ]->
  [ St_B_2(B, id, ltkB, pkA, A, gx) ]

// Step 2.1: B sends g^y to A
rule B_2_send:
  let m = 'g'^~y
      K_m = h(gx^~y)
  in
  [ St_B_2(B, id, ltkB, pkA, A, gx)
  , Fr(~y)
  ]
--[ Send(B, m) 
  , Running(B, A, <'I', 'R', K_m>)
  ]->
  [ St_B_3(B, id, ltkB, pkA, A, gx, ~y, K_m)
  , Out(m)
  ]

// Step 2.2: A receives g^y from B
rule A_2_receive:
  let m = gy
      K_m = h(gy^x)
  in
  [ St_A_2(A, id, ltkA, pkB, B, x)
  , In(m)
  ]
--[ Recv(A, m) 
  , Running(A, B, <'R', 'I', K_m>)
  ]->
  [ St_A_3(A, id, ltkA, pkB, B, x, gy, K_m) ]

// Step 3.1: A sends signature and MAC to B
rule A_3_send:
  let m = <A, sign(<gy, 'g'^x>, ltkA), senc(<'0', A>, K_m), pk(ltkA)>
  in
  [ St_A_3(A, id, ltkA, pkB, B, x, gy, K_m) ]
--[ Send(A, m) ]->
  [ St_A_4(A, id, ltkA, pkB, B, x, gy, K_m)
  , Out(m)
  ]

// Step 3.2: B receives signature and MAC from A
rule B_3_receive:
  let m = <A, signature, senc(<'0', A>, K_m), pkA>
  in
  [ St_B_3(B, id, ltkB, pkA, A, gx, y, K_m) 
  , In(m)
  ]
--[ Recv(B, m)
  , Equal(verify(signature, <'g'^y, gx>, pkA), true)
  ]->
  [ St_B_4(B, id, ltkB, pkA, A, gx, y, K_m) ]

// Step 4.1: B sends signature and MAC to A
rule B_4_send:
  let m = <B, sign(<gx, 'g'^y>, ltkB), senc(<'1', B>, K_m), pk(ltkB)>
  in
  [ St_B_4(B, id, ltkB, pkA, A, gx, y, K_m) ]
--[ Send(B, m) 
  , Secret(B, K_m)
  , Commit(B, A, <'R', 'I', K_m>)
  , Honest(A)
  , Honest(B)
  ]->
  [ St_B_5(B, id, ltkB, pkA, A, gx, y, K_m)
  , Out(m)
  ]

// Step 4.2: A receives signature and MAC from B
rule A_4_receive:
  let m = <B, signature, senc(<'1', B>, K_m), pkB>
  in
  [ St_A_4(A, id, ltkA, pkB, B, x, gy, K_m)
  , In(m)
  ]
--[ Recv(A, m)
  , Equal(verify(signature, <'g'^x, gy>, pkB), true)
  , Finish(A, B)
  , Secret(A, K_m)
  , Commit(A, B, <'I', 'R', K_m>)
  , Honest(A)
  , Honest(B)
  ]->
  [ St_A_5(A, id, ltkA, pkB, B, x, gy, K_m) ]

/*** Restrictions ***/
restriction Equality:
  "All a b #i.
  Equal(a, b) @i ==> a = b
  "

/*** Lemmas ***/
// Find intended execution trace - VERIFIES
lemma executable:
  exists-trace
    "Ex a b #i. Finish(a, b) @i & not (a = b)
    // Only one instance of each agent
    & (All c d id1 id2 #j #k. Create_A(c, id1) @j & Create_A(d, id2) @k ==> id1 = id2)
    & (All e f id3 id4 #l #m. Create_B(e, id3) @l & Create_B(f, id4) @m ==> id3 = id4)
    // No compromised agents
    & not(Ex X #n. Reveal(X) @n)
    "

// Secrecy of K_m for A and B - VERIFIES
lemma secrecy:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// Perfect forward secrecy of K_m - VERIFIES
lemma secrecy_PFS:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i & r < i)"

// A agrees with B on K_m (and each other's identities) - VERIFIES
lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// B agrees with A on K_m - FALSIFIES
lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

end