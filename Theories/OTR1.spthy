theory OTR1
begin

builtins: 
  diffie-hellman, 
  signing,
  hashing

/*** Key Infrastructure ***/

/* Register signing/verification key pair. 
 * ltk is the secret signing key
 * pk(ltk) is the public verification key
 */
rule Register_key_pair:
  [ Fr(~ltk) ]
--[]->
  [ !Ltk($A, ~ltk)
  , !Pk($A, pk(~ltk))
  , Out(pk(~ltk))
  ]

// Agent is compromised and its secret signing key is revealed
rule Reveal_ltk:
  [ !Ltk(A, ltk) ]
--[ Reveal(A) ]->
  [ Out(ltk) ]

/*** Formalise OTR Authentic Key-Exchange ***
 * 1. A -> B : g^x, {g^x}sk_A, vk_A
 * 2. B -> A : g^y, {g^y}sk_B, vk_B
 */

// Initialise thread for initiator role
rule Init_A:
  [ Fr(~id) 
  , !Ltk(A, ltkA) // A's signing key
  , !Pk(B, pkB)   // B's verification key
  ]
--[ Create_A(A, ~id) ]->
  [ St_A_1(A, ~id, ltkA, pkB, B) ]

// Initialise thread for responder role
rule Init_B:
  [ Fr(~id)
  , !Ltk(B, ltkB) // B's signing key
  , !Pk(A, pkA)   // A's verification key
  ]
--[ Create_B(B, ~id) ]->
  [ St_B_1(B, ~id, ltkB, pkA, A) ]

// Step 1.1: A sends g^x to B
rule A_1_send:
  let m = <'g'^~x, sign('g'^~x, ltkA), pk(ltkA)>
  in
  [ St_A_1(A, id, ltkA, pkB, B) 
  , Fr(~x)
  ]
--[ Send(A, m) ]->
  [ St_A_2(A, id, ltkA, pkB, B, ~x)
  , Out(m)
  ]

// Step 1.2: B receives g^x from A
rule B_1_receive:
  let m = <gx, signature, pkA>
  in
  [ St_B_1(B, id, ltkB, pkA, A)
  , In(m)
  ]
--[ Recv(B, m) 
  , Equal(verify(signature, gx, pkA), true) // Check signature
  ]->
  [ St_B_2(B, id, ltkB, pkA, A, gx) ]

// Step 2.1: B sends g^y to A
rule B_2_send:
  let m = <'g'^~y, sign('g'^~y, ltkB), pk(ltkB)>
      K = h(gx^~y)
  in
  [ St_B_2(B, id, ltkB, pkA, A, gx)
  , Fr(~y)
  ]
--[ Send(B, m) 
  , Secret(B, K)
  , Honest(B)
  , Honest(A)
  ]->
  [ St_B_3(B, id, ltkB, pkA, A, gx, ~y, K)
  , Out(m)
  ]

// Step 2.2: A receives g^y from B
rule A_2_receive:
  let m = <gy, signature, pkB>
      K = h(gy^x)
  in
  [ St_A_2(A, id, ltkA, pkB, B, x)
  , In(m)
  ]
--[ Recv(A, m)
  , Equal(verify(signature, gy, pkB), true)
  , Secret(A, K)
  , Honest(A)
  , Honest(B) 
  , Finish(A, B)
  ]->
  [ St_A_3(A, id, ltkA, pkB, B, x, gy, K) ]

/*** Restrictions ***/
restriction Equality:
  "All a b #i.
  Equal(a, b) @i ==> a = b
  "

/*** Lemmas ***/
// Find intended execution trace - VERIFIES
lemma executable:
  exists-trace
    "Ex a b #i. Finish(a, b) @i & not (a = b)
    // Only one instance of each agent
    & (All c d id1 id2 #j #k. Create_A(c, id1) @j & Create_A(d, id2) @k ==> id1 = id2)
    & (All e f id3 id4 #l #m. Create_B(e, id3) @l & Create_B(f, id4) @m ==> id3 = id4)
    & not(Ex X #n. Reveal(X) @n)
    "

end